* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
* THIS PROGRAM CONTRASTS THE EFFICIENCY OF SCAN
* WITH DYNAMIC AND NON DYNAMIC VARIABLES.
* FOR THIS COMPARISON, THE TEST VALUE
* DOES NOT EXIST IN OUR STRING.
*
DEFINE DATA LOCAL
1 #STRING (A15) INIT <'QWERTASDFGZXCVB'>
1 #DYN (A) DYNAMIC
1 #LOOP (P7)
1 #NUM (I2)
1 #CPU-START (I4)
1 #CPU-ELAPSED (I4)
END-DEFINE
*
INCLUDE AATITLER
INCLUDE AASETC
*
WRITE 5T 'DATA FOR THIS PROGRAM' /
      5T '*********************' //
      5T '#STRING IS:' #STRING /
      5T 'TARGET, XX, IS NOT PRESENT'
*
MOVE *CPU-TIME TO #CPU-START
SETC. SETTIME
FOR #LOOP = 1 TO 1000000
IGNORE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE // 5T 'TIME FOR DUMMY FOR LOOP: '
   30T 'CLOCK' *TIMD (SETC.) (NL=3) 'CPU' #CPU-ELAPSED (NL=3)
*
MOVE *CPU-TIME TO #CPU-START
SETA. SETTIME
FOR #LOOP = 1 TO 1000000
IF #STRING = SCAN 'XX'
   IGNORE
   END-IF
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE // 5T 'TIME FOR SCAN "XX":'
      30T  'CLOCK' *TIMD (SETA.) (NL=3) 'CPU' #CPU-ELAPSED (NL=3)
*
COMPRESS #STRING INTO #DYN
MOVE *CPU-TIME TO #CPU-START
SETB. SETTIME
FOR #LOOP = 1 TO 1000000
IF #DYN = SCAN 'XX'
   IGNORE
   END-IF
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE // 5T 'TIME FOR DYNAMIC:'
    30T 'CLOCK' *TIMD (SETB.) (NL=3) 'CPU' #CPU-ELAPSED (NL=3)
*
END


    PAGE #   1                    DATE:    10-03-15
    PROGRAM: SCAN03               LIBRARY: INSIDE

    DATA FOR THIS PROGRAM
    *********************

    #STRING IS: QWERTASDFGZXCVB
    TARGET, XX, IS NOT PRESENT


    TIME FOR DUMMY FOR LOOP: CLOCK   14 CPU  131


    TIME FOR SCAN 'XX':      CLOCK   16 CPU  164


    TIME FOR DYNAMIC:        CLOCK   17 CPU  171
