* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
* THIS PROGRAM WILL ALLOW THE INSERTION AND
* DELETION OF TEXT FROM A VARIABLE, AS WELL
* AS CHANGES TO THE TEXT.
*
DEFINE DATA LOCAL
1 #TEXT (A490)
1 REDEFINE #TEXT
  2 #TEXT-ARRAY (A70/1:7)
1 #TEXT-ARRAY-OLD (A70/1:7)
1 REDEFINE #TEXT-ARRAY-OLD
  2 #TEXT-OLD (A490)
1 #SUB (I2)
1 #POSITION (I2)
1 #POSITION-PLUS1 (I2)
1 #INSERT (A70)
1 #DEL1 (I2)
1 #DEL2 (I2)
END-DEFINE
*
SET KEY PF1 NAMED 'FINI' PF2 NAMED 'MOD'
        PF3 NAMED 'INSRT' PF4 NAMED 'DEL1' PF5 NAMED 'DEL2'
REPEAT
MOVE #TEXT-ARRAY (*) TO #TEXT-ARRAY-OLD (*)
INPUT (AD=M IP=OFF)  3/5 #TEXT-ARRAY (1)
             / 5T #TEXT-ARRAY (2)
             / 5T #TEXT-ARRAY (3)
             / 5T #TEXT-ARRAY (4)
             / 5T #TEXT-ARRAY (5)
             / 5T #TEXT-ARRAY (6)
             / 5T #TEXT-ARRAY (7) //
             'PF1 WHEN FINISHED' /
             'PF3 TO INSERT CHARACTERS AFTER CURSOR' /
             'PF4 TO START A DELETE AFTER CURSOR' /
             'PF5 TO END A DELETE BEFORE CURSOR' /
             'PF2 TO MODIFY THE STRING'
*
IF *PF-NAME = 'FINI'
    ESCAPE BOTTOM
    END-IF
*
IF *PF-NAME = 'MOD' AND #TEXT NE #TEXT-OLD
   ESCAPE TOP
   END-IF
******
IF *PF-NAME = 'INSRT'
*
   COMPUTE #SUB = *CURS-LINE - 2
   COMPUTE #POSITION = ( #SUB - 1 ) * 70 + ( *CURS-COL - 5 )
   COMPUTE #POSITION-PLUS1 = #POSITION + 1
*
INPUT NO ERASE (IP=OFF) 17/10 'ENTER INSERTED TEXT ON NEXT LINE' / #INSERT
COMPRESS SUBSTRING (#TEXT,1,#POSITION) #INSERT SUBSTRING (#TEXT,#POSITION-PLUS1)
         INTO #TEXT LEAVING NO SPACE
ESCAPE TOP
END-IF
******
IF *PF-NAME = 'DEL1'
   COMPUTE #SUB = *CURS-LINE - 2
   COMPUTE #DEL1 = (#SUB - 1 ) * 70 + ( *CURS-COL - 5 )
   ESCAPE TOP
   END-IF
*******
IF *PF-NAME = 'DEL2'
   COMPUTE #SUB = *CURS-LINE - 2
   COMPUTE #DEL2 = (#SUB - 1 ) * 70 + ( *CURS-COL - 5 )
   ADD 1 TO #DEL2
*
   COMPRESS SUBSTRING (#TEXT,1,#DEL1) SUBSTRING (#TEXT,#DEL2)
            INTO #TEXT LEAVING NO SPACE
   ESCAPE TOP
   END-IF
END-REPEAT
*
END



