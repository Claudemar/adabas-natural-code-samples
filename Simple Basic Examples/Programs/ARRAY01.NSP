* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
* PROGRAM DEMONSTRATES THE "COST" OF NATBOL
* CODE FOR COPYING AN ARRAY
*
DEFINE DATA LOCAL
1 #CPU-START (I4)
1 #CPU-ELAPSED (I4)
1 #LOOP (I4)
1 #LOOP2 (I4)
1 #A (A3/1:100)
1 #B (A3/1:100)
END-DEFINE
*
INCLUDE AATITLER
INCLUDE AASETC
*
MOVE *CPU-TIME TO #CPU-START
SETA. SETTIME
FOR #LOOP = 1 TO 1000000
IGNORE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'DUMMY FOR LOOP: ' *TIMD (SETA.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETB. SETTIME
FOR #LOOP = 1 TO 1000000
    FOR #LOOP2 = 1 TO 100
    MOVE #A (#LOOP2) TO #B (#LOOP2)
    END-FOR
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'FOR LOOP ' *TIMD (SETB.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETC. SETTIME
FOR #LOOP = 1 TO 1000000
MOVE #A (*) TO #B (*)
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'ASTERISKS: ' *TIMD (SETC.) #CPU-ELAPSED
*
END

    PAGE #   1                    DATE:    10-05-16
    PROGRAM: ARRAY01              LIBRARY: INSIDE

    DUMMY FOR LOOP:         3          34
    FOR LOOP      1448       10460
    ASTERISKS:       150        1487
