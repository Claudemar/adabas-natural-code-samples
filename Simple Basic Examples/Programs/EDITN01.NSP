* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
* THIS PROGRAM SHOWS ABOUT THE SIMPLEST EDIT MASKS
* THAT EXIST FOR NUMERIC FIELDS. THEY WILL
* BE USED IN BOTH MOVE EDITED COMMANDS
* AND WRITE STATEMENTS.
*
* IN THIS EXAMPLE, THE NUMERIC FIELD WILL
* BE AN INTEGER (N5, SEE BELOW)
*
* ALSO, THE NUMERIC FIELD WILL BE POSITIVE
*
DEFINE DATA LOCAL
1 #NUMERIC (N5) INIT <123>
1 #ALPHA (A8)
END-DEFINE
*
INCLUDE AATITLER
INCLUDE AASETC
*
WRITE 5T 'DATA FOR THIS PROGRAM' /
5T '??????????' /
5T '#NUMERIC IS:' #NUMERIC /
5T 'WHICH IN HEX IS:' #NUMERIC (EM=H(5))
*
WRITE // 5T '#NUMERIC WITH EM=99999:'
#NUMERIC (EM=99999)
/ 5T '#NUMERIC WITH EM=ZZZZ9:'
#NUMERIC (EM=ZZZZ9)
*
MOVE EDITED #NUMERIC (EM=99999) TO #ALPHA
WRITE // 5T 'AFTER: MOVE EDITED #NUMERIC'
'(EM=99999) TO #ALPHA'
/ 5T '#ALPHA IS:' #ALPHA 5X 'IN HEX:' #ALPHA (EM=H(8))
*
MOVE EDITED #NUMERIC (EM=ZZZZ9) TO #ALPHA
WRITE / 5T 'AFTER: MOVE EDITED #NUMERIC'
' (EM=ZZZZ9) TO #ALPHA'
/ 5T '#ALPHA IS:' #ALPHA 5X 'IN HEX:' #ALPHA (EM=H(8))
*
MOVE #NUMERIC TO #ALPHA
WRITE / 5T 'AFTER: MOVE #NUMERIC TO #ALPHA'
/ 5T '#ALPHA IS:' #ALPHA 5X 'IN HEX:' #ALPHA (EM=H(8))
*
MOVE 123 TO #ALPHA
WRITE / 5T 'AFTER: MOVE 123 TO #ALPHA'
/ 5T '#ALPHA IS:' #ALPHA 5X 'IN HEX:' #ALPHA (EM=H(8))
*
END



    PAGE #   1                    DATE:    10-06-05
    PROGRAM: EDITN01              LIBRARY: INSIDE

    DATA FOR THIS PROGRAM
    ??????????
    #NUMERIC IS:    123
    WHICH IN HEX IS: 3030313233


    #NUMERIC WITH EM=99999: 00123
    #NUMERIC WITH EM=ZZZZ9:   123


    AFTER: MOVE EDITED #NUMERIC (EM=99999) TO #ALPHA
    #ALPHA IS: 00123        IN HEX: 3030313233202020

    AFTER: MOVE EDITED #NUMERIC  (EM=ZZZZ9) TO #ALPHA
    #ALPHA IS:   123        IN HEX: 2020313233202020

    AFTER: MOVE #NUMERIC TO #ALPHA
    #ALPHA IS: 123          IN HEX: 3132332020202020

    AFTER: MOVE 123 TO #ALPHA
    #ALPHA IS: 123          IN HEX: 3132332020202020
