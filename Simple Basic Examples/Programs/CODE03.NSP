* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
* THIS PROGRAM COMPARES THE TIMES FOR
* TWO WAYS TO "CONVERT" A ONE CHARACTER CODE
* TO A ONE CHARACTER CLASS
*
DEFINE DATA LOCAL
1 #CODE (A1) INIT <'G'>
1 #CODES (A20)   INIT <'BCFJMNADRTXYZEGSVW'>
1 #CLASSES (A20) INIT <'AAAAAABBBBCCCDDDDD'>
1 REDEFINE #CLASSES
  2 #VAL (A1/1:20)
1 #CLASS (A1)
1 #POS (I4)
1 #LOOP (I4)
1 #CPU-START (I4)
1 #CPU-ELAPSED (I4)
END-DEFINE
*
include aatitler
include aasetc
*********************************
** FIRST, THE DECIDE
**
MOVE *CPU-TIME TO #CPU-START
SETA. SETTIME
FOR #LOOP = 1 TO 100000
DECIDE ON FIRST VALUE OF #CODE
  VALUE 'B', 'C', 'F', 'J', 'M', 'N'
      MOVE 'A' TO #CLASS
  VALUE 'A', 'D', 'R', 'T'
      MOVE 'B' TO #CLASS
  VALUE 'X', 'Y', 'Z'
      MOVE 'C' TO #CLASS
  VALUE 'E', 'G', 'S', 'V', 'W'
      MOVE 'D' TO #CLASS
  NONE
  WRITE 'ERROR'
END-DECIDE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'DECIDE TIMES: ' *TIMD (SETA.) #CPU-ELAPSED
******** EXAMINE  *************
MOVE *CPU-TIME TO #CPU-START
SETB. SETTIME
FOR #LOOP = 1 TO 100000
EXAMINE #CODES FOR #CODE GIVING POSITION #POS
MOVE #VAL (#POS) TO #CLASS
END-FOR
*
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'EXAMINE TIMES: ' *TIMD (SETB.) #CPU-ELAPSED
**************** DECIDE *****************
MOVE *CPU-TIME TO #CPU-START
SETC. SETTIME
FOR #LOOP = 1 TO 100000
DECIDE ON FIRST VALUE OF #CODE
  VALUE 'B', 'C', 'F', 'J', 'M', 'N'
      MOVE 'A' TO #CLASS
  VALUE 'A', 'D', 'R', 'T'
      MOVE 'B' TO #CLASS
  VALUE 'X', 'Y', 'Z'
      MOVE 'C' TO #CLASS
  VALUE 'E', 'G', 'S', 'V', 'W'
      MOVE 'D' TO #CLASS
  NONE
  WRITE 'ERROR'
END-DECIDE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'DECIDE TIMES: ' *TIMD (SETC.) #CPU-ELAPSED
********* DUMMY FOR LOOP *********
MOVE *CPU-TIME TO #CPU-START
SETD. SETTIME
FOR #LOOP = 1 TO 100000
IGNORE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'FOR LOOP TIMES: ' *TIMD (SETD.) #CPU-ELAPSED
END


    PAGE #   1                    DATE:    11/21/10
    PROGRAM: CODE03               LIBRARY: INSIDE

    DECIDE TIMES:         4          38
    EXAMINE TIMES:         2          15
    DECIDE TIMES:         3          36
    FOR LOOP TIMES:         1           3
