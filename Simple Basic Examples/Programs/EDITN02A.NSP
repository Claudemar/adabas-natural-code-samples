* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
* THIS PROGRAM SHOWS A POTENTIAL PROBLEM WHEN COMPARING
* AN ALPHA FIELD WITH A NUMERIC FIELD THAT HAS BEEN
* REDEFINED AS ALPHA..
*
DEFINE DATA LOCAL
1 #NUMERIC (N5) INIT <1234>
1 REDEFINE #NUMERIC
2 #NUM-ALPHA (A5)
1 #ALPHA (A5)
END-DEFINE
*
INCLUDE AATITLER
INCLUDE AASETC
*
MOVE  #NUMERIC TO #ALPHA
WRITE / 5T 'AFTER: MOVE EDITED #NUMERIC(EM=ZZZZ9)TO #ALPHA'
// 5T '#ALPHA IS:'#ALPHA 5X 'IN HEX:'#ALPHA (EM=H(8))
// 5T '#NUMERIC, WITH AN EDIT MASK OF ZZZZ9 IS'
#NUMERIC (EM=ZZZZ9)
/ 5T 'BUT IN HEX IS'#NUMERIC (EM=HHHHH)
/ 5T '-'(40) /
*
WRITE 5T 'ALTHOUGH THE VALUES OF #ALPHA AND #NUMERIC'
/ 5T 'LOOK THE SAME, NAMELY ?1234?'
/ 5T 'THEIR HEX VALUES ARE DIFFERENT'/
/ 5T 'HENCE, WHEN WE DO: IF #ALPHA = #NUM-ALPHA'
/ 5T 'THE RESULT IS AS SHOWN BELOW'
*
IF #ALPHA = #NUM-ALPHA
WRITE // 5T '#ALPHA = #NUM-ALPHA'
ELSE
WRITE // 5T '#ALPHA NE #NUM-ALPHA'
END-IF
*
END

    PAGE #   1                    DATE:    10-06-05
    PROGRAM: EDITN02A             LIBRARY: INSIDE


    AFTER: MOVE EDITED #NUMERIC(EM=ZZZZ9)TO #ALPHA

    #ALPHA IS: 1234      IN HEX: 3132333420

    #NUMERIC, WITH AN EDIT MASK OF ZZZZ9 IS  1234
    BUT IN HEX IS 3031323334
    ----------------------------------------

    ALTHOUGH THE VALUES OF #ALPHA AND #NUMERIC
    LOOK THE SAME, NAMELY ?1234?
    THEIR HEX VALUES ARE DIFFERENT

    HENCE, WHEN WE DO: IF #ALPHA = #NUM-ALPHA
    THE RESULT IS AS SHOWN BELOW


    #ALPHA NE #NUM-ALPHA
