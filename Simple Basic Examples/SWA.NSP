0010* Ralph Zbrog
0020* LEN Consulting LLC
0030*
0040* Sample program to use the Source Work Area as a TEMP file
0050* (ie  emulate a WORK file)
0060*
0070************************************************************************
0080*
0090DEFINE DATA LOCAL
0100                                            /* As defined in USR2014P:
01101 C-SCRMAX           (I01)   CONST <10>     /* Maximum number of lines
01201 C-SCRLEN           (I02)   CONST <133>    /* Length of data area
01301 C-PRFLEN           (I01)   CONST <06>     /* Length of prefix area
01401 SHOW-PREFIX-AREA   (L)     CONST <FALSE>
0150*
01601 CONTROL-BLOCK
0170  2 ED-SESNUM        (B02)   /* Number of session (0-50)
0180  2 ED-LOGFLAG       (A01)   /* Logical name in use
0190  2 ED-LOGNAME       (A08)   /* Logical name of the session
0200  2 ED-REQUEST       (A04)   /* Editor request
0210  2 ED-LIBRARY       (A08)   /* Unused
0220  2 ED-MEMBER        (A08)   /* Unused
0230  2 ED-RECLEN        (B02)   /* Maximum length of each line
0240  2 ED-PRFLEN        (B02)   /* Length of a prefix line on screen
0250  2 ED-SCRLEN        (B02)   /* Length of a single line on screen
0260  2 ED-SCRNUM        (B02)   /* Number of lines on screen
0270  2 ED-SCRMAX        (B02)   /* Maximum number of lines on screen
0280  2 ED-RENUM         (B01)   /* Unused
0290  /*
0300  2 ED-MCMND         (A50)   /* Main command: command line
0310  2 ED-SCMND         (A08)   /* Secondary command: PF-key
0320  2 ED-CAREA         (B02)   /* Cursor area
0330  2 ED-CROW          (B02)   /* Cursor row
0340  2 ED-CCLM          (B02)   /* Cursor column
0350  /*
0360  2 ED-RECTOP        (B02)   /* Unused
0370  2 ED-NEW-RECTOP    (B04)   /* Line number of new first line on screen
0380  2 ED-RECNUM        (B02)   /* Unused
0390  2 ED-NEW-RECNUM    (B04)   /* Number of lines in the source area
0400  2 ED-FILECHG       (B01)   /* Indicator if source area has been mod.
0410  2 ED-SRAUSED       (B04)   /* Used source area in bytes
0420  2 ED-SRAFREE       (B04)   /* Unused source area in bytes
0430  2 ED-FLINE         (A80)   /* Message test. Explanation for
0440                             /* ED-MSGNUM or ED-ERRNUM
0450  2 ED-MSGNUM        (B02)   /* Message number
0460  2 ED-ERRNUM        (B02)   /* Error number
0470  2 ED-DUMMY         (A32)   /* Used for enhancements
0480                   2 REDEFINE ED-DUMMY
0490    3 ED-DSTYPE      (A01)   /* Type of session
0500    3 ED-RECMAX      (B02)   /* Target session for CSES request
0510*
05201 CB-ALIN                         /* Attributes of prefix area
0530  2 CB-ALIN-V        (N01/1:C-SCRMAX)
05401 L-PREFIXES                      /* Prefix area
0550  2 L-PREFIX-MAX     (1:C-SCRMAX)
0560    3 L-PREFIX       (A06)        /* Length must be equal C-PRFLEN
0570                   3 REDEFINE L-PREFIX
0580      4 L-PREFIX-A1  (A01/1:C-PRFLEN)
0590*
06001 CB-ADAT                         /* Attributes of data lines
0610  2 CB-ADAT-V        (N01/1:C-SCRMAX)
06201 L-DATA-LINES                    /* Data lines
0630  2 L-DATA-MAX       (1:C-SCRMAX)
0640    3 L-DATA         (A133)       /* Length must be equal C-SCRLEN
0650                   3 REDEFINE L-DATA
0660      4 L-DATA-A1    (A01/1:C-SCRLEN)
0670*
06801 C-SESNUM (B2)
06901 ERROR-NUMBER (N4)
0700*
0710************************************************************************
0720*
07301 #I (I4)
07401 #ED-SCRNUM (I4)
07501 #RECNUM (I4)
07601 #LINES (P7)
07701 #WORK (A133)     1 REDEFINE #WORK    /* WORK record
0780  2 #W (A2)
0790  2 FILLER 1X
0800  2 #ALPHA (A65/2)
08101 #WORDS (P7)
0820END-DEFINE
0830                                       /* Initialize the SWA
0840SET CONTROL 'Z'                        /* Clear SWA
0850FORMAT (3) LS=134
0860DEFINE PRINTER (3) OUTPUT 'SOURCE'
0870EJECT OFF (3)
0880WRITE (3) NOTITLE '00 '
0890                - '....+....1....+....2....+....3....+....4....+....5'
0900                - '....+....6....+....7....+....8....+....9....+....0'
0910                - '....+....1....+....2....+....3'
0920WRITE (3) NOTITLE '07 A penny saved is a penny earned.'
0930WRITE (3) NOTITLE '09 The quick brown fox jumps over the lazy dog.'
0940WRITE (3) NOTITLE '06 That"s all water under the bridge.'
0950*
0960*
0970RESET  ED-SESNUM                       /* Open a free session
0980ASSIGN ED-LOGFLAG = 'N'
0990ASSIGN ED-DSTYPE  = 'N'                /* NATURAL SWA
1000ASSIGN ED-RECLEN  = 133                /* Length must be equal C-SCRLEN
1010ASSIGN ED-SCRMAX  = 10
1020ASSIGN ED-SCRNUM  = 10
1030ASSIGN ED-SCRLEN  = C-SCRLEN
1040ASSIGN ED-PRFLEN  = C-PRFLEN
1050*
1060ASSIGN ED-REQUEST = 'OPEN'             /* Open session
1070CALLNAT 'USR2014N' CONTROL-BLOCK
1080                   CB-ALIN
1090                   L-PREFIX-A1(*,*)
1100                   CB-ADAT
1110                   L-DATA-A1(*,*)
1120IF  ED-ERRNUM <> 0
1130  THEN
1140    ERROR-NUMBER := ED-ERRNUM          /* Error number in decimal form
1150    WRITE 'Error occurred during OPEN processing:' (I)
1160        / '=' ERROR-NUMBER
1170        / '=' ED-FLINE (AL=65)
1180    STOP
1190END-IF
1200ASSIGN C-SESNUM = ED-SESNUM       /* Session number returned from OPEN
1210ASSIGN #RECNUM  = ED-NEW-RECNUM   /* Number of lines in SWA
1220*
1230*
1240ASSIGN ED-REQUEST = 'GETL'             /* Get lines to ...
1250FOR #I = 1 #RECNUM                     /*   emulate READ WORK ...
1260  ASSIGN ED-NEW-RECTOP = #I
1270  CALLNAT 'USR2014N' CONTROL-BLOCK
1280                     CB-ALIN
1290                     L-PREFIX-A1(*,*)
1300                     CB-ADAT
1310                     L-DATA-A1(*,*)
1320  ASSIGN #WORK = L-DATA (1)
1330  DISPLAY
1340          'SWA/Line' #I (EM=ZZ9)
1350          '/Words'   #W
1360          '/Line'    #ALPHA (*)
1370  IF  #W IS (N2)
1380    THEN
1390      ADD 1 TO #LINES
1400      ADD VAL (#W) TO #WORDS
1410  END-IF
1420END-FOR
1430*
1440*
1450WRITE
1460  /// #RECNUM (SG=F NL=3) 'source lines'
1470   // #LINES  (SG=F NL=3) 'lines processed'
1480    / #WORDS  (SG=F NL=3) 'words'
1490*
1500ASSIGN ED-REQUEST = 'CLSS'             /* Close session
1510CALLNAT 'USR2014N' CONTROL-BLOCK
1520                   CB-ALIN
1530                   L-PREFIX-A1(*,*)
1540                   CB-ADAT
1550                   L-DATA-A1(*,*)
1560SET CONTROL 'Z'                        /* Clear SWA
1570END
