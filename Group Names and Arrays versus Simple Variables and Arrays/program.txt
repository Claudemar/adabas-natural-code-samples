                                                        

* THIS PROGRAM IS BASICALLY JUST INFORMATIONAL.
* THERE WILL BE SEVERAL SUBSEQUENT SNIPPETS
* THAT UTILIZE THE INFORMATION PROVIDED HERE.
*
* WE WILL BE COMPARING/CONTRASTING A GROUP NAME
* WITH TWO "SUB VARIABLES", WITH A VARIABLE
* THAT HAS BEEN REDEFINED INTO TWO "SUB VARIABLES".
* 
* NEXT WE WILL CONTRAST A GROUP ARRAY WITH TWO
* "SUB VARIABLES" AND TWO VARIABLES THAT ARE EACH ARRAYS.
*
* ***** IMPORTANT  ******
* YOU SHOULD COME AWAY FROM THIS PROGRAM
* WITH THE REALIZATION THAT A GROUP NAME IS
* MERELY AN ABBREVIATION.
*
* YES, THERE ARE ALSO SOME STATEMENTS THAT PERTAIN
* TO JUST GROUP NAMES (E.G. MOVE BY NAME, MOVE BY POSITION
* ETC.). WE WILL BE ADDRESSING THEM IN A SUBSEQUENT SNIPPET.
* 
* THERE IS ALSO ONE STATEMENT WHERE A GROUP NAME IS 
* NOT AN ABBREVIATION. THIS WILL ALSO BE COVERED
* IN A SUBSEQUENT SNIPPET.
*
DEFINE DATA LOCAL
1 #GROUP 
  2 #A (A3) INIT <'ABC'>
  2 #B (A3) INIT <'DEF'>
1 #FIELD (A6) INIT <'ABCDEF'>
1 REDEFINE #FIELD
  2 #C (A3) 
  2 #D (A3)
1 #TARGET (A50)  
********************
1 #GROUP-ARRAY (3)
  2 #GA (A3) INIT <'A1A','A2A','A3A'>
  2 #GB (A3) INIT <'B1B','B2B','B3B'>
1 REDEFINE #GROUP-ARRAY
  2 #STRING (A18)

END-DEFINE
*
INCLUDE AATITLER
INCLUDE AASETC
*
WRITE 5T '=' #GROUP / 5T '=' #FIELD //
      5T 'THE COMPILER BASICALLY REPLACES #GROUP IN THE WRITE' /
      5T 'WITH #A #B. HENCE, THE SPACE BETWEEN ABC AND DEF.' / 
      5T '#FIELD IS A VARIABLE, HENCE ABCDEF IS CONTIGUOUS.'     
*
WRITE 5T '*' (40)
*
SKIP 1
*
COMPRESS '***' #GROUP '***' INTO #TARGET WITH DELIMITER ';'
WRITE 5T '=' #TARGET //
      5T 'NOTE THAT THE COMPILER "REPLACES" #GROUP IN THE COMPRESS' /
      5T 'WITH #A #B, HENCE THE OUTPUT IS IDENTICAL TO WHAT' /
      5T 'WE WOULD SEE WITH COMPRESS "***" #A #B "***"' /
      5T 'THUS THERE IS A SEMI COLON BETWEEN ABC AND DEF'
*
WRITE 5T '*' (40)
*
SKIP 1
*
COMPRESS '***' #FIELD '***' INTO #TARGET WITH DELIMITER ';'
WRITE 5T '=' #TARGET //
      5T 'SINCE #FIELD IS JUST A VARIABLE, ' /
      5T 'ABC AND DEF ARE CONTIGUOUS' /
*
NEWPAGE
*
WRITE 5T 'HERE IS OUTPUT FROM WRITE #GROUP-ARRAY (*)'
      // 5T #GROUP-ARRAY (*) /
         5T 'NOTE THAT NATURAL HAS "REPLACED"' /
         5T '#GROUP-ARRAY (*) WITH #GA(*) #GB(*)'  /
         5T '*'(30)
      / 5T 'HERE IS OUTPUT FROM WRITE #GA (*) #GB (*)'
      // 5T #GA (*) #GB (*) / 5T '*' (30)
      / 5T 'HERE IS #STRING, WHICH IS A MEMORY "DUMP"'
      // 5T #STRING / 5T '*' (30) /
*
COMPRESS '***' #GROUP-ARRAY (*) '***' INTO #TARGET WITH DELIMITER ';'
WRITE 5T '=' #TARGET //
      5T 'NOTE THAT THE COMPILER "REPLACES" #GROUP-ARRAY (*) IN THE COMPRESS' /
      5T 'WITH #GA(*) #GB(*), HENCE THE OUTPUT IS IDENTICAL TO WHAT' /
      5T 'WE WOULD SEE WITH COMPRESS "***" #GA (*) #GB (*) "***"' /
      5T 'THUS THERE IS A SEMI COLON BETWEEN ALL THE GA"S AND GB"S'
*      
END
 

    PAGE #   1                    DATE:    02/18/11
    PROGRAM: GROUP01              LIBRARY: NEWPROGS
 
    #GROUP: ABC DEF
    #FIELD: ABCDEF
 
    THE COMPILER BASICALLY REPLACES #GROUP IN THE WRITE
    WITH #A #B. HENCE, THE SPACE BETWEEN ABC AND DEF.
    #FIELD IS A VARIABLE, HENCE ABCDEF IS CONTIGUOUS.
    ****************************************
 
    #TARGET: ***;ABC;DEF;***
 
    NOTE THAT THE COMPILER 'REPLACES' #GROUP IN THE COMPRESS
    WITH #A #B, HENCE THE OUTPUT IS IDENTICAL TO WHAT
    WE WOULD SEE WITH COMPRESS '***' #A #B '***'
    THUS THERE IS A SEMI COLON BETWEEN ABC AND DEF
    ****************************************
 
    #TARGET: ***;ABCDEF;***
 
    SINCE #FIELD IS JUST A VARIABLE,
    ABC AND DEF ARE CONTIGUOUS
    
    ****************************************************
    
    PAGE #   2                    DATE:    02/18/11
    PROGRAM: GROUP01              LIBRARY: NEWPROGS
 
    HERE IS OUTPUT FROM WRITE #GROUP-ARRAY (*)
 
    A1A A2A A3A B1B B2B B3B
    NOTE THAT NATURAL HAS 'REPLACED'
    #GROUP-ARRAY (*) WITH #GA(*) #GB(*)
    ******************************
    HERE IS OUTPUT FROM WRITE #GA (*) #GB (*)
 
    A1A A2A A3A B1B B2B B3B
    ******************************
    HERE IS #STRING, WHICH IS A MEMORY 'DUMP'
 
    A1AB1BA2AB2BA3AB3B
    ******************************
 
    #TARGET: ***;A1A;A2A;A3A;B1B;B2B;B3B;***
 
    NOTE THAT THE COMPILER 'REPLACES' #GROUP-ARRAY (*) IN THE COMPRESS
    WITH #GA(*) #GB(*), HENCE THE OUTPUT IS IDENTICAL TO WHAT
    WE WOULD SEE WITH COMPRESS '***' #GA (*) #GB (*) '***'
    THUS THERE IS A SEMI COLON BETWEEN ALL THE GA'S AND GB'S                                        