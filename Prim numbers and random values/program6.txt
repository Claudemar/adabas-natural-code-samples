* Subprogram .. DEAL
* Author ...... Lukas Hundemer
* Last edit ... 2006-07-20
* Copyright ... Software AG 2006. All rights reserved.
*
* Description
* Mix numbers randomly like dealing cards
*
DEFINE DATA
PARAMETER
1 #P-MAX (I4)     /* number of entries
1 #P-NUM (I4 / *) /* random numbers from 1 to #P-MAX
1 #RSP (I4)                /*
LOCAL
1 #I (I4)
1 #J (I4)
1 #MAX1 (I4)
1 #NUM (I4)
1 #NUM1 (I4)
1 #NEXT (I4 / 0:*)
1 #DICE (I4)
1 #DICE1 (I4)
END-DEFINE
EXPAND OCCURRENCES OF ARRAY #NEXT TO (0:#P-MAX)
#MAX1 := #P-MAX - 1
FOR #I = 0 TO #MAX1
  #NEXT (#I) := #I + 1
END-FOR

FOR #I = 1 TO #P-MAX
  #J := #P-MAX + 1 - #I
  #DICE := DICE (<#J>)
  #DICE1 := #DICE - 1
  #NUM := 0
  IF #DICE > 1
    FOR #J = 1 TO #DICE1
      #NUM := #NEXT(#NUM)
    END-FOR
  END-IF
  #P-NUM (#I) := #NEXT(#NUM)
  #NUM1 := #NEXT(#NUM)
  #NEXT(#NUM) := #NEXT (#NUM1)
*   WRITE '=' #I '=' #DICE '=' #P-NUM(#I)
*   FOR #J = 0 TO #P-MAX
*     WRITE #J '-->' #NEXT(#J)
*   END-FOR
END-FOR            