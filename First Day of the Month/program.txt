                                                        

* THIS PROGRAM SHOWS TWO WAYS TO CREATE  
* THE FIRST DAY OF THE MONTH FOR A GIVEN DATE.
*
* IT IS ASSUMED THAT THE TWO SOURCE DATES (D AND A FORMATS)
* HAVE BEEN VALIDATED PRIOR TO THIS CODE BEING EXECUTED.
* 
* THE FIRST WAY IS QUITE TRIVIAL AND ASSUMES
* THE SOURCE DATE AND THE TARGET DATE ARE ALPHA   
*
* THE SECOND WAY ASSUMES THE SOURCE AND TARGET DATES
* ARE BOTH FORMATTED AS "D" (DATE).
*
* SINCE THE MOVE EDITED STATEMENT SUFFICES TO CONVERT
* DATES IN ALPHA FORMAT TO DATE FORMAT, AND VISA VERSA,
* THE CODE BELOW CAN BE USED AS FIRST STEPS IN
* GOING FROM AN ALPHA SOURCE DATE TO A DATE FORMAT TARGET
* AND A DATE FORMAT SOURCE TO AN ALPHA TARGET DATE.
*
DEFINE DATA LOCAL
1 #DATE-ALPHA-SOURCE (A8) INIT <'20110225'>
1 #DATE-ALPHA-TARGET (A8)
1 REDEFINE #DATE-ALPHA-TARGET
  2 FILLER 6X
  2 #DAY (A2)
* 
1 #DATE-D-SOURCE (D)  INIT <D'2011-02-05'>
1 #DATE-D-TARGET (D)
1 #DAYS-A (A2)
1 REDEFINE #DAYS-A
  2 #DAYS-N (N2)
END-DEFINE
*
* TWO COPYCODE MEMBERS FOLLOW. THEY ARE SHOWN IN THE
* DESCRIPTION FOR THIS CODE. 
* AATITLER IS SIMPLY A WRITE TITLE STATEMENT.
* AASETC ROUTES OUTPUT TO THE EDITOR AREA
* (MAKES CUT & PASTE OPERATIONS SIMPLER).
*
INCLUDE AATITLER
INCLUDE AASETC
*
* FIRST WE WILL USE SUBSTRING TO CREATE DAY 01
*
MOVE #DATE-ALPHA-SOURCE TO #DATE-ALPHA-TARGET  
MOVE '01' TO SUBSTRING (#DATE-ALPHA-TARGET,7,2)
WRITE 5T '=' #DATE-ALPHA-SOURCE (EM=XXXX'-'XX'-'XX) / 
      5T '=' #DATE-ALPHA-TARGET (EM=XXXX'-'XX'-'XX) //
*
* NOW WE WILL USED OUR REDEFINE'D #DAY FOR THE MOVE
* IN GENERAL, IT IS FAR MORE EFFICIENT TO USE SUCH
* A REDEFINE'D VARIABLE THAN TO USE SUBSTRING.
* IT IS ALWAYS A PERFORMANCE ERROR TO HAVE A SUBSTRING
* WHERE THE SECOND AND THIRD ARGUMENTS ARE FIXED, AS
* THEY ARE ABOVE (THE SEVEN AND TWO).
*
MOVE #DATE-ALPHA-SOURCE TO #DATE-ALPHA-TARGET  
MOVE '01' TO #DAY
WRITE 5T '=' #DATE-ALPHA-SOURCE (EM=XXXX'-'XX'-'XX) /
      5T '=' #DATE-ALPHA-TARGET (EM=XXXX'-'XX'-'XX) //
*
* NOW FOR SOME FUN. MOST PROGRAMMERS ARE FAMILIAR WITH EDIT MASKS
* LIKE YYYY, MM, AND DD. HERE WE WILL USE DD.
*
MOVE EDITED #DATE-D-SOURCE (EM=DD) TO #DAYS-A
COMPUTE #DATE-D-TARGET = #DATE-D-SOURCE - #DAYS-N + 1
WRITE 5T '=' #DATE-D-SOURCE (EM=YYYY-MM-DD) / 
      5T '=' #DATE-D-TARGET (EM=YYYY-MM-DD)
*
END

Here is the output from the program above.

  
    PAGE #   1                    DATE:    11-02-13
    PROGRAM: DATE01               LIBRARY: NEWPROGS
 
    #DATE-ALPHA-SOURCE: 2011-02-25
    #DATE-ALPHA-TARGET: 2011-02-01
 
 
    #DATE-ALPHA-SOURCE: 2011-02-25
    #DATE-ALPHA-TARGET: 2011-02-01
 
 
    #DATE-D-SOURCE: 2011-02-05
    #DATE-D-TARGET: 2011-02-01                                        