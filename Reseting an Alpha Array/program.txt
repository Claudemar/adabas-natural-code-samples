                                                        

* THIS PROGRAM COMPARES THREE APPROACHES TO
* SETTING AN ALPHA ARRAY TO NULL VALUES.
*
* THE THREE APPROACHES ARE:
* 1) RESET #ARRAY(*)
* 2) RESET #A (SINGLE VARIABLE THAT IS A REDEFINE OF #ARRAY
* 3) MOVE ' ' TO #ARRAY (*)
*
DEFINE DATA LOCAL
1 #ARRAY (A15/1:100)
1 REDEFINE #ARRAY
 2 #A (A1500)
1 #LOOP (I4)
1 #CPU-START (I4)
1 #CPU-ELAPSED (I4)
END-DEFINE

INCLUDE AATITLER
INCLUDE AASETC

MOVE *CPU-TIME TO #CPU-START
SETA. SETTIME
FOR #LOOP = 1 TO 1000000
RESET #ARRAY (*)
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'ARRAY TIME' *TIMD (SETA.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETB. SETTIME
FOR #LOOP = 1 TO 1000000
RESET #A
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'STRING TIME' *TIMD (SETB.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETC. SETTIME
FOR #LOOP = 1 TO 1000000
MOVE ' ' TO #ARRAY (*)
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'MOVE BLANK TIME' *TIMD (SETC.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETD. SETTIME
FOR #LOOP = 1 TO 1000000
IGNORE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'DUMMY FOR' *TIMD (SETD.) #CPU-ELAPSED
*

END

****************************************************
PC output

    PAGE #   1                    DATE:    11-02-12
    PROGRAM: ARRAY02              LIBRARY: NEWPROGS
 
    ARRAY TIME       12         119
    STRING TIME        5          44
    MOVE BLANK TIME      249        2488
    DUMMY FOR        3          25

*****************************************************    
Mainframe output    

    PAGE #   1                    DATE:    11-02-12
    PROGRAM: RESET02              LIBRARY: XSTRO
 
    ARRAY TIME       13         102
    STRING TIME       11          97
    MOVE BLANK TIME       20         158
    DUMMY FOR        6          61                                        