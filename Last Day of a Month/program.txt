                                                        

 * THIS PROGRAM SHOWS HOW TO START WITH A SOURCE DATE
* THAT IS EITHER ALPHA OR DATE FORMAT, AND DERIVE THE
* LAST DAY OF THE MONTH THAT PRECEDES THE SOURCE DATE.
*
DEFINE DATA LOCAL
1 #DATE-ALPHA-SOURCE (A8) INIT <'20110225'>
1 #DATE-ALPHA-TARGET (A8)
1 REDEFINE #DATE-ALPHA-TARGET
  2 FILLER 6X
  2 #DAY (A2)
* 
1 #DATE-D-SOURCE (D)  INIT <D'2011-02-05'>
1 #DATE-D-TARGET (D)
1 #DATE-D-INTERMEDIATE (D)
1 #DAYS-A (A2)
1 REDEFINE #DAYS-A
  2 #DAYS-N (N2)
END-DEFINE
*
INCLUDE AATITLER
INCLUDE AASETC
*
* FIRST, WE WILL START WITH A DATE FORMAT SOURCE.
* OUR FIRST STEP, SHOWN IN THE PROGRAM DATE01, WILL
* BE TO DERIVE THE FIRST DAY OF THE MONTH FOR THE
* SOURCE DATE. THEN WE WILL SUBTRACT ONE FROM THE
* "INTERMEDIATE" DATE TO PRODUCE THE LAST DAY
* OF THE PRECEDING MONTH.
*
MOVE EDITED #DATE-D-SOURCE (EM=DD) TO #DAYS-A
COMPUTE #DATE-D-INTERMEDIATE = #DATE-D-SOURCE - #DAYS-N + 1
COMPUTE #DATE-D-TARGET = #DATE-D-INTERMEDIATE - 1
WRITE 5T '=' #DATE-D-SOURCE (EM=YYYY-MM-DD) / 
      5T '=' #DATE-D-INTERMEDIATE (EM=YYYY-MM-DD) /
      5T '=' #DATE-D-TARGET (EM=YYYY-MM-DD) //
*
* OF COURSE, YOU REALIZE THAT IF WE DO NOT REQUIRE
* THE INTERMEDIATE DATE FOR ANYTHING, WE CAN COMBINE
* THE TWO COMPUTE STATEMENTS INTO ONE, AS SHOWN BELOW.
*
MOVE EDITED #DATE-D-SOURCE (EM=DD) TO #DAYS-A
COMPUTE #DATE-D-TARGET = #DATE-D-SOURCE - #DAYS-N 
WRITE 5T '=' #DATE-D-SOURCE (EM=YYYY-MM-DD) / 
      5T '=' #DATE-D-TARGET (EM=YYYY-MM-DD) //
*
* NOW WE WILL WORK WITH AN ALPHA SOURCE. FIRST, WE WILL
* CREATE THE FIRST DAY OF THE MONTH IN ALPHA FORMAT.
* THEN WE WILL CONVERT FROM ALPHA TO DATE FORMAT.
* FINALLY, WE WILL SUBTRACT ONE FROM THE DATE FORMAT
* TO PRODUCE THE DESIRED LAST DAY OF THE PREVIOUS MONTH.
*
MOVE #DATE-ALPHA-SOURCE TO #DATE-ALPHA-TARGET  
MOVE '01' TO #DAY   /* THIS GIVES US DAY 1 OF CURRENT MONTH
MOVE EDITED #DATE-ALPHA-TARGET 
     TO #DATE-D-TARGET (EM=YYYYMMDD)
SUBTRACT 1 FROM #DATE-D-TARGET
WRITE 5T '=' #DATE-ALPHA-SOURCE (EM=XXXX-XX-XX) / 
      5T '=' #DATE-D-TARGET (EM=YYYY-MM-DD) //    
*
* IF WE REQUIRE AN ALPHA OUTPUT, ONLY ONE MORE
* MOVE EDITED STATEMENT IS REQUIRED TO CONVERT
* OUR DATE FORMAT OUTPUT TO ALPHA FORMAT OUTPUT:
*
MOVE EDITED #DATE-D-TARGET (EM=YYYYMMDD) TO #DATE-ALPHA-TARGET
WRITE 5T '=' #DATE-ALPHA-SOURCE (EM=XXXX-XX-XX) / 
      5T '=' #DATE-ALPHA-TARGET (EM=XXXX-XX-XX) //         
END   

********************************************************
Output from the program above
   
    PAGE #   1                    DATE:    11-02-13
    PROGRAM: DATE02               LIBRARY: NEWPROGS
 
    #DATE-D-SOURCE: 2011-02-05
    #DATE-D-INTERMEDIATE: 2011-02-01
    #DATE-D-TARGET: 2011-01-31
 
 
    #DATE-D-SOURCE: 2011-02-05
    #DATE-D-TARGET: 2011-01-31
 
 
    #DATE-ALPHA-SOURCE: 2011-02-25
    #DATE-D-TARGET: 2011-01-31
 
 
    #DATE-ALPHA-SOURCE: 2011-02-25
    #DATE-ALPHA-TARGET: 2011-01-31                                       