                                                        

* THIS PROGRAM COMPARES THREE APPROACHES TO
* SETTING A NUMERIC ARRAY TO NULL VALUES.
*
* THE THREE APPROACHES ARE:
* 1) RESET #ARRAY(*)
* 2) MOVE ALL H'F0' TO #A (SINGLE VARIABLE THAT IS A REDEFINE OF #ARRAY
* 3) MOVE 0 TO #ARRAY (*)
*
DEFINE DATA LOCAL
1 #ARRAY (N5/1:100)
1 REDEFINE #ARRAY
 2 #A (A500)
1 #ZEROES (A500) INIT FULL LENGTH <H'F0'> 
1 #LOOP (I4)
1 #CPU-START (I4)
1 #CPU-ELAPSED (I4)
END-DEFINE

INCLUDE AATITLER
INCLUDE AASETC

MOVE *CPU-TIME TO #CPU-START
SETA. SETTIME
FOR #LOOP = 1 TO 1000000
RESET #ARRAY (*)
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'ARRAY TIME' 25T *TIMD (SETA.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETB. SETTIME
FOR #LOOP = 1 TO 1000000
MOVE ALL H'F0' TO #A
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'MOVE ALL TIME' 25T *TIMD (SETB.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETE. SETTIME
FOR #LOOP = 1 TO 1000000
MOVE #ZEROES TO #A
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'MOVE ALL VARIANT' 25T *TIMD (SETE.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETC. SETTIME
FOR #LOOP = 1 TO 1000000
MOVE 0 TO #ARRAY (*)
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'MOVE ZERO TIME' 25T *TIMD (SETC.) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETD. SETTIME
FOR #LOOP = 1 TO 1000000
IGNORE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 5T 'DUMMY FOR' 25T *TIMD (SETD.) #CPU-ELAPSED
*
END

***************************************************
PC output

    PAGE #   1                    DATE:    02/20/11
    PROGRAM: ARRAY03              LIBRARY: NEWPROGS
 
    ARRAY TIME                11         116
    MOVE ALL TIME              4          42
    MOVE ALL VARIANT           5          50
    MOVE ZERO TIME           272        2709
    DUMMY FOR                  3          25
    
****************************************************
Mainframe output    

    PAGE #   1                    DATE:    02/20/11
    PROGRAM: ARRAY03              LIBRARY: XSTRO
 
    ARRAY TIME                 9          83
    MOVE ALL TIME             11         102
    MOVE ALL VARIANT          11         101
    MOVE ZERO TIME            16         149
    DUMMY FOR                  6          60                                            